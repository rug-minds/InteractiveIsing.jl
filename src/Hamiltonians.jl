module Hamiltonians
using ..InteractiveIsing
using ..InteractiveIsing: branchSim


"""
Add factors to factor array defined in module scope
"""
macro addfactor(facnames...)
    for name in facnames
        push!(factors,eval(name))
    end
end

"""
Struct to define factors to be used in hamiltonian
expr: A string in julia syntax for the factor
symb: Define a unique symbol for the factor, if two factors use the same symbol
    a value can be used to pick any of the factors to be added over the others
val: Value for the symbol to pick one factor instead of the others
loop: Defines wether the factor is present in the loop or not. - more explanation needed -
"""
struct hFactor{T}
    expr::String
    symb::Symbol
    val::T
    loop::Bool
end

function getUniqueParams(factors)
    # Make a set of parameters for hamiltonian, ordered
    # by the order they are added by the macro addfactor
    paramset = tuple()
    for factor in factors
        symb = factor.symb
        for el in paramset
            if el == symb
                @goto cont
            end
        end
        paramset = (paramset..., symb)
        @label cont
    end
    return  paramset
end

"""
Generate a struct of the type: struct HType{Symbs, Vals} end
"""
function generateHType(vals...)
    symbs = getUniqueParams(factors)

    if length(vals) > length(symbs)
        error("Cannot be higher than number of symbols")
    end

    vals = (vals...,repeat([false], length((length(vals)+1):length(symbs)))...)

    return HType{symbs,vals}()

end
export generateHType

function editHType(htype::HType{Symbs, Vals}, vals...) where {Symbs, Vals}

    if length(vals) > length(Symbs)
        error("Cannot be higher than number of symbols")
    end

    vals = (vals..., Vals[(length(vals)+1):end]...)

    return HType{Symbs,vals}()
end

function editHType(htype::HType{Symbs, Vals}, pairs::Pair ...) where {Symbs, Vals}
    if length(pairs) > length(Symbs)
        error("Cannot be higher than number of symbols")
    end

    vals = [Vals...]

    for pair in pairs
        for (idx,symb) in enumerate(Symbs)
            if pair[1] == symb
                vals[idx] = pair[2]
                @goto nextpair
            end
        end
        @label nextpair
    end

    vals = Tuple(vals)

    return HType{Symbs,vals}()
end
export editHType

function editHType!(g, pairs...)
    g.htype = editHType(g.htype, pairs...)
end
export editHType!

export setSimHType
function setSimHType(sim, pairs...; prt = false)
    sim.g.htype = editHType(sim.g.htype, pairs...)
    if prt
        println(sim.g.htype)
    end
    branchSim(sim)
end

function paramIdx(htype::HType{Symbs,Vals}, symb) where {Symbs,Vals}
    idx = 1
    for symbol in Symbs
        if symb == symbol
            break
        end
        idx +=1
    end

    if idx > length(Symbs)
        error("Symbol not found")
        idx = length(Symbs)
    end

    return idx
end

"""
Get value of parameter of HType
"""
function getHParam(htype::HType{Symbs,Vals}, symb) where {Symbs, Vals}
    idx = paramIdx(htype, symb)

    return Vals[idx]
end

getHParam(g, symb) = getHParam(g.htype, symb)
export getHParam


"""
Finds the expression that fits with the given symbol, value of the symbol,
and wether the factor is part of the loop or not
"""
function findExpr(symb, val, loop)
    for factor in factors
        if factor.symb == symb && factor.val == val && factor.loop == loop
            return factor.expr
        end
    end
    return ""
end

"""
Makes expression in form of string out of a set of symbols, values, 
and a predicate that tells the function wether the expression is part of
a loop over neighbors or not
"""
function buildExpr(loop, symbs, vals)
    str = string()

    for (idx, symb) in enumerate(symbs)
        term = findExpr(symb, vals[idx], loop)
        if term != ""
            if str != ""
                str *= " + "
            end
            str *= ("(@inbounds " * term * ")")
        end
        
    end
    return str
end

function getEFacExpr(g, idx, htype::Type{HType{Symbs,Vals}}) where {Symbs, Vals}
    exprvec = []

    line = "for conn in g.adj[idx] \n efactor +="
    line *= buildExpr(true, Symbs, Vals)

    line *= "end"

    push!(exprvec, Meta.parse(line))

    line = "return efactor"

    normalfactor = buildExpr(false, Symbs, Vals)

    line *= normalfactor != "" ? "+ "*normalfactor : ""

    push!(exprvec, Meta.parse(line))

    expr = Expr(
        :block,
        :(efactor = 0),
        exprvec...
    ) 
end
export getEFacExpr

"""
Get the energy factor (where we define E === σ_i Σ_j fac_j) for the state
the function is dispatched on the graph g, the idx i and the type of the Hamiltonian
which may be generated by the function generateHType(Symbs...).
"""
@generated function getEFactor(g, idx, htype::HType{Symbs,Vals}) where {Symbs, Vals}
    exp = getEFacExpr(g, idx, htype)
    return exp
end
export getEFactor

unweightedloop = hFactor("-g.state[connIdx(conn)]" , :Weighted, false, true)
weightedloop = hFactor("-connW(conn)*g.state[connIdx(conn)]", :Weighted, true, true)
magfac = hFactor("-g.d.mlist[idx]", :MagField, true, false)
clampfac = hFactor("g.d.clampfac[idx]*g.state[idx]", :Clamp, true, false)
defects = hFactor("", :Defects, false, false)

factors = [];

@addfactor unweightedloop weightedloop magfac clampfac defects

end